plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '2.25.0'
    id 'com.github.gmazzo.buildconfig' version '3.0.3'
    id 'com.adarshr.test-logger' version '3.2.0'
    id "io.sentry.jvm.gradle" version "4.4.0"
}

sentry {
  // Generates a JVM (Java, Kotlin, etc.) source bundle and uploads your source code to Sentry.
  // This enables source context, allowing you to see your source
  // code as part of your stack traces in Sentry.
  includeSourceContext = true

  org = "girc"
  projectName = "gir-launcher-java"
  authToken = System.getenv("SENTRY_AUTH_TOKEN")
}

repositories {
    mavenCentral()
    maven {
        url = uri('https://repo.u-team.info')
    }

    maven {
        url = uri('https://jitpack.io')
    }
}

version = '1.1.0-dev'

ext {
    junitVersion = '5.9.2'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'eu.girc.launcher'
    mainClass = 'eu.girc.launcher.Launcher'
}

javafx {
    version = '22'
    modules = ['javafx.controls', 'javafx.web']
}

buildConfig {
    useJavaOutput()
    packageName('eu.girc.launcher')
    buildConfigField('String', 'VERSION', "\"${project.version}\"")
}

// Make Sentry compatible with Build Configs
tasks.generateSentryBundleIdJava.dependsOn(tasks.generateBuildConfig)

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'org.apache.commons:commons-compress:1.26.1'
    implementation 'net.hycrafthd:minecraft_authenticator:3.0.6'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.guava:guava:32.1.2-jre'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()

    testlogger {
        theme 'mocha-parallel'
    }
}

jlink {
    options.set(['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--ignore-signing-information'])
    forceMerge("log4j-api")
    jpackage {
        vendor = "German-Immersive-Railroading-Community and Contributors"

        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console']
        }
    }
}