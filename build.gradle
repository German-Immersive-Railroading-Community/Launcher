import java.nio.file.Paths

plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
    id 'com.github.gmazzo.buildconfig' version '3.0.3'
    id 'com.adarshr.test-logger' version '3.2.0'
}

repositories {
    mavenCentral()
    maven {
        url = uri('https://repo.u-team.info')
    }

    maven {
        url = uri('https://jitpack.io')
    }
}

version = '1.1.0'

ext {
    junitVersion = '5.9.2'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'eu.girc.launcher'
    mainClass = 'eu.girc.launcher.Launcher'
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.web']
}

buildConfig {
    useJavaOutput()
    packageName('eu.girc.launcher')
    buildConfigField('String', 'VERSION', "\"${project.version}\"")

    String path = ""

    if (org.gradle.internal.os.OperatingSystem.current().windows) {
        path = Paths.get(System.getProperty("user.home"), "AppData", "Roaming").toString()
    } else if (org.gradle.internal.os.OperatingSystem.current().linux || org.gradle.internal.os.OperatingSystem.current().macOsX) {
        path = Paths.get(System.getProperty("user.home"), ".local", "share").toString()
    }

    buildConfigField('String', 'APP_DATA', path)
}

dependencies {
    implementation 'com.github.Querz:NBT:6.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'
//    implementation 'net.hycrafthd:minecraft_authenticator:3.0.5'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.json:json:20220320'

    implementation files('../Minecraft-Authenticator/build/libs/minecraft_authenticator-3.0.5.jar')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()

    testlogger {
        theme 'mocha-parallel'
    }
}

jlink {
    options.set(['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages'])
    forceMerge("log4j-api")
    jpackage {
        vendor = "German-Immersive-Railroading-Community and Contributors"
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console']
        }
    }
}